module Animation.Sub.InterruptingAnimations.FreezeAxis.Main exposing (main)

import Anim.Builder exposing (AnimBuilder)
import Anim.Engine.Sub as Sub
import Anim.Property.Translate as Translate
import Browser
import Browser.Dom as Dom
import Browser.Events
import Html exposing (Html, div, text)
import Html.Attributes exposing (class, id, style)
import Html.Events exposing (onClick)
import Motion.Easing as Easing exposing (Easing(..))
import Task



-- MAIN


main : Program () Model Msg
main =
    Browser.element
        { init = init
        , update = update
        , view = view
        , subscriptions = subscriptions
        }



-- MODEL


animGroupName : String
animGroupName =
    "movingBox"


canvasId : String
canvasId =
    "anim-canvas"


type alias Model =
    { animState : Sub.AnimState
    , canvasW : Float
    , canvasH : Float
    , xPos : XPos
    , yPos : YPos
    , pendingMove : Maybe MoveIntent
    , isReady : Bool
    }


type MoveIntent
    = MoveToLeft
    | MoveToRight
    | MoveToTop
    | MoveToBottom


type XPos
    = XLeft
    | XCenter
    | XRight


type YPos
    = YTop
    | YCenter
    | YBottom


boxPct : Float
boxPct =
    12


boxWidthPx : Float -> Float
boxWidthPx canvasW =
    (canvasW * boxPct) / 100


boxHeightPx : Float -> Float
boxHeightPx canvasH =
    (canvasH * boxPct) / 100


init : () -> ( Model, Cmd Msg )
init _ =
    ( { animState =
            Sub.init
                [ Translate.initXY animGroupName 0 0 ]
      , canvasW = 0
      , canvasH = 0
      , xPos = XCenter
      , yPos = YCenter
      , pendingMove = Nothing
      , isReady = False
      }
    , measureCanvas
    )


measureCanvas : Cmd Msg
measureCanvas =
    Task.attempt GotCanvas (Dom.getViewportOf canvasId)



-- POSITION HELPERS


targetX : XPos -> Float -> Float
targetX pos w =
    let
        widthPx =
            boxWidthPx w
    in
    case pos of
        XLeft ->
            0

        XCenter ->
            (w - widthPx) / 2

        XRight ->
            w - widthPx


targetY : YPos -> Float -> Float
targetY pos h =
    let
        heightPx =
            boxHeightPx h
    in
    case pos of
        YTop ->
            0

        YCenter ->
            (h - heightPx) / 2

        YBottom ->
            h - heightPx



-- ANIMATIONS


moveBoxX : Float -> AnimBuilder mode -> AnimBuilder mode
moveBoxX x =
    moveBox <|
        Translate.toX x


moveBoxY : Float -> AnimBuilder mode -> AnimBuilder mode
moveBoxY y =
    moveBox <|
        Translate.toY y


moveBox : (Translate.Builder mode -> Translate.Builder mode) -> AnimBuilder mode -> AnimBuilder mode
moveBox moveFunc =
    Translate.for animGroupName
        >> moveFunc
        >> Translate.speed 200
        >> Translate.easing BounceOut
        >> Translate.build


snapBoxXY : Float -> Float -> AnimBuilder mode -> AnimBuilder mode
snapBoxXY x y =
    Translate.for animGroupName
        >> Translate.toXY x y
        >> Translate.build



-- UPDATE


type Msg
    = GotAnimationUpdate Sub.AnimMsg
    | MoveLeft
    | MoveRight
    | MoveUp
    | MoveDown
    | Resize
    | GotCanvas (Result Dom.Error Dom.Viewport)


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        GotAnimationUpdate animationMsg ->
            let
                ( newAnimState, _ ) =
                    Sub.update animationMsg model.animState
            in
            ( { model | animState = newAnimState }
            , Cmd.none
            )

        ---8<-- [start:WithFreeze]
        MoveLeft ->
            ( { model
                | pendingMove = Just MoveToLeft
              }
            , measureCanvas
            )

        MoveRight ->
            ( { model
                | pendingMove = Just MoveToRight
              }
            , measureCanvas
            )

        MoveUp ->
            ( { model
                | pendingMove = Just MoveToTop
              }
            , measureCanvas
            )

        MoveDown ->
            ( { model
                | pendingMove = Just MoveToBottom
              }
            , measureCanvas
            )

        ---8<-- [end:WithFreeze]
        Resize ->
            ( model, measureCanvas )

        GotCanvas (Ok element) ->
            let
                w =
                    element.viewport.width

                h =
                    element.viewport.height

                modelWithSize =
                    { model
                        | canvasW = w
                        , canvasH = h
                        , pendingMove = Nothing
                        , isReady = True
                    }
            in
            case model.pendingMove of
                Just MoveToLeft ->
                    ( { modelWithSize
                        | xPos = XLeft
                        , animState =
                            Sub.animate model.animState <|
                                Sub.freezeY [ Sub.translate ]
                                    >> moveBoxX (targetX XLeft w)
                      }
                    , Cmd.none
                    )

                Just MoveToRight ->
                    ( { modelWithSize
                        | xPos = XRight
                        , animState =
                            Sub.animate model.animState <|
                                Sub.freezeY [ Sub.translate ]
                                    >> moveBoxX (targetX XRight w)
                      }
                    , Cmd.none
                    )

                Just MoveToTop ->
                    ( { modelWithSize
                        | yPos = YTop
                        , animState =
                            Sub.animate model.animState <|
                                Sub.freezeX [ Sub.translate ]
                                    >> moveBoxY (targetY YTop h)
                      }
                    , Cmd.none
                    )

                Just MoveToBottom ->
                    ( { modelWithSize
                        | yPos = YBottom
                        , animState =
                            Sub.animate model.animState <|
                                Sub.freezeX [ Sub.translate ]
                                    >> moveBoxY (targetY YBottom h)
                      }
                    , Cmd.none
                    )

                Nothing ->
                    ( { modelWithSize
                        | animState =
                            Sub.animate model.animState <|
                                snapBoxXY (targetX model.xPos w) (targetY model.yPos h)
                      }
                    , Cmd.none
                    )

        GotCanvas (Err _) ->
            ( model, Cmd.none )



-- SUBSCRIPTIONS


subscriptions : Model -> Sub.Sub Msg
subscriptions model =
    Sub.batch
        [ Sub.subscriptions GotAnimationUpdate model.animState
        , Browser.Events.onResize (\_ _ -> Resize)
        ]



-- VIEW


view : Model -> Html Msg
view model =
    let
        button bgColor label onClickMsg =
            Html.button
                [ onClick onClickMsg
                , class "ui-action-button"
                , style "background-color" bgColor
                ]
                [ text label ]

        moveLeftButton =
            button "#007BFF" "Move Left" MoveLeft

        moveRightButton =
            button "#28A745" "Move Right" MoveRight

        moveUpButton =
            button "#6F42C1" "Move Up" MoveUp

        moveDownButton =
            button "#FFC107" "Move Down" MoveDown

        box =
            div
                (Sub.attributes animGroupName model.animState
                    ++ [ style "width" (String.fromFloat (boxWidthPx model.canvasW) ++ "px")
                       , style "height" (String.fromFloat (boxHeightPx model.canvasH) ++ "px")
                       , style "background-color" "#FF5733"
                       , style "border-radius" "8px"
                       , style "position" "absolute"
                       , style "top" "0"
                       , style "left" "0"
                       , style "visibility"
                            (if model.isReady then
                                "visible"

                             else
                                "hidden"
                            )
                       ]
                )
                []
    in
    div [ class "example-stage" ]
        [ text ""
        , div [ class "example-controls" ]
            [ moveLeftButton
            , moveRightButton
            , moveUpButton
            , moveDownButton
            ]
        , div [ id canvasId, class "example-canvas--fluid" ]
            [ box ]
        ]
