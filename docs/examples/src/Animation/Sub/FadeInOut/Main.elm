module Animation.Sub.FadeInOut.Main exposing (main)

import Anim.Builder exposing (AnimBuilder)
import Anim.Engine.Sub as Sub
import Anim.Property.Opacity as Opacity
import Browser
import Html exposing (Html, button, div, text)
import Html.Attributes exposing (class, id, style)
import Html.Events exposing (onClick)



-- MAIN


main : Program () Model Msg
main =
    Browser.element
        { init = \_ -> init
        , view = view
        , update = update
        , subscriptions = subscriptions
        }



-- MODEL
---8<-- [start:model]


type alias Model =
    { animState : Sub.AnimState }


init : ( Model, Cmd Msg )
init =
    ( { animState =
            Sub.init
                [ Opacity.init animGroup 0 ]
      }
    , Cmd.none
    )



---8<-- [end:model]
-- ANIMATION
---8<-- [start:build]


animGroup : String
animGroup =
    "fadeAnim"


fadeTo : Float -> AnimBuilder mode -> AnimBuilder mode
fadeTo to =
    Opacity.for animGroup
        >> Opacity.to to
        >> Opacity.duration 2500
        >> Opacity.build


fadeIn : AnimBuilder mode -> AnimBuilder mode
fadeIn =
    fadeTo 1


fadeOut : AnimBuilder mode -> AnimBuilder mode
fadeOut =
    fadeTo 0



---8<-- [end:build]
-- UPDATE
---8<-- [start:Msg]


type Msg
    = GotSubMsg Sub.AnimMsg
      ---8<-- [end:Msg]
    | TriggerFadeIn
    | TriggerFadeOut



---8<-- [start:update]


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        GotSubMsg subMsg ->
            let
                ( newAnimState, _ ) =
                    Sub.update subMsg model.animState
            in
            ( { model | animState = newAnimState }
            , Cmd.none
            )

        ---8<-- [end:update]
        ---8<-- [start:trigger]
        TriggerFadeIn ->
            ( { model | animState = Sub.animate model.animState fadeIn }
            , Cmd.none
            )

        TriggerFadeOut ->
            ( { model | animState = Sub.animate model.animState fadeOut }
            , Cmd.none
            )



---8<-- [end:trigger]
-- SUBSCRIPTIONS
---8<-- [start:subscriptions]


subscriptions : Model -> Sub Msg
subscriptions model =
    Sub.subscriptions GotSubMsg model.animState



---8<-- [end:subscriptions]
-- VIEW


view : Model -> Html Msg
view model =
    div
        [ class "example-stage"
        , style "text-align" "center"
        ]
        [ div [ class "example-controls" ]
            [ button
                [ onClick TriggerFadeIn
                , class "ui-action-button primary"
                ]
                [ text "Fade In" ]
            , button
                [ onClick TriggerFadeOut
                , class "ui-action-button primary"
                ]
                [ text "Fade Out" ]
            ]

        ---8<-- [start:render]
        , div
            (Sub.attributes animGroup model.animState
                ++ [ style "background-color" "red"
                   , style "border-radius" "8px"
                   , style "width" "100%"
                   , style "flex" "1 1 auto"
                   , style "min-height" "0"
                   ]
            )
            []

        ---8<-- [end:render]
        ]
