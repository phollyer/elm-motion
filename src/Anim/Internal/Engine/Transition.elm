module Anim.Internal.Engine.Transition exposing
    ( AnimBuilder
    , AnimEvent(..)
    , AnimMsg
    , AnimState
    , EngineBuilder
    , TimelineBuilder
    , animate
    , attributes
    , events
    , eventsStopPropagation
    , init
    , reset
    , retarget
    , startingStyleNode
    , startingStyleNodeFor
    , stop
    , update
    )

import Anim.Extra.TransformOrder exposing (TransformProperty)
import Anim.Internal.Builder as Builder
import Anim.Internal.Engine.CSS.CSS as CSS exposing (AnimState(..))
import Anim.Internal.Engine.CSS.Styles as Styles exposing (Styles)
import Anim.Internal.Engine.Shared.AnimGroups as AnimGroups exposing (AnimGroups)
import Anim.Internal.Engine.Shared.PlayState as PlayState
import Anim.Internal.Engine.Transition.AnimGroup as AnimGroup exposing (AnimGroup)
import Anim.Internal.Engine.Transition.Generator as Generator exposing (AnimGroupName)
import Anim.Internal.Engine.Transition.Styles as TransitionStyles
import Anim.Internal.Extra.Color as Color exposing (Color(..))
import Anim.Internal.Property.Opacity as Opacity
import Anim.Internal.Property.PerspectiveOrigin as PerspectiveOrigin
import Anim.Internal.Property.Rotate as Rotate
import Anim.Internal.Property.Scale as Scale
import Anim.Internal.Property.Size as Size
import Anim.Internal.Property.Skew as Skew
import Anim.Internal.Property.Translate as Translate
import Dict
import Html exposing (Html)
import Html.Attributes



-- ============================================================
-- TYPES
-- ============================================================


type alias AnimState =
    CSS.AnimState Builder.ForTransitionEngine AnimGroup


type alias AnimGroupName =
    String


type alias AnimBuilder mode =
    CSS.AnimBuilder mode


type alias TimelineBuilder engine =
    CSS.TimelineBuilder engine


type alias EngineBuilder =
    TimelineBuilder Builder.ForTransitionEngine



-- ============================================================
-- INITIALIZE
-- ============================================================


init : List (EngineBuilder -> EngineBuilder) -> AnimState
init =
    let
        initGroup : EngineBuilder -> AnimGroupName -> Builder.AnimGroupConfig -> AnimGroup
        initGroup builder _ { properties } =
            Generator.init
                (Builder.discreteTransitionsEnabled builder)
                (Builder.getDiscreteEntryProperties builder)
                (Builder.getDiscreteExitProperties builder)
                properties
    in
    CSS.init initGroup



-- ============================================================
-- TRIGGER
-- ============================================================


animate : AnimState -> (EngineBuilder -> EngineBuilder) -> AnimState
animate =
    let
        generateAnimGroup : Maybe (List TransformProperty) -> EngineBuilder -> AnimGroupName -> { a | properties : List Builder.ProcessedPropertyConfig } -> AnimGroup
        generateAnimGroup _ builder _ { properties } =
            Generator.generateAnimation
                (Builder.discreteTransitionsEnabled builder)
                (Builder.getDiscreteEntryProperties builder)
                (Builder.getDiscreteExitProperties builder)
                properties
                |> AnimGroup.setStartingStyles (extractStartingStyles properties)

        insertAnimGroup : AnimGroups Builder.ProcessedAnimGroupConfig -> AnimGroupName -> AnimGroup -> AnimGroups AnimGroup -> AnimGroups AnimGroup
        insertAnimGroup animGroupsConfig animGroupName newAnimGroup acc =
            case AnimGroups.get animGroupName acc of
                Nothing ->
                    AnimGroups.insert animGroupName newAnimGroup acc

                Just currentGroup ->
                    let
                        styles =
                            AnimGroups.get animGroupName animGroupsConfig
                                |> Maybe.map (.properties >> toCssPropertyNames)
                                |> Maybe.withDefault []
                                |> AnimGroup.mergeStyles newAnimGroup currentGroup
                    in
                    AnimGroups.insert animGroupName styles acc
    in
    CSS.animate AnimGroup.setPlayState generateAnimGroup insertAnimGroup


{-| Re-anchor an animation to a new target by snapping to the new end values.

The Transition engine is a thin wrapper over CSS transitions, with no
JavaScript-side runtime snapshot of the currently rendered values - it
only knows the previous _target_, not where the element actually is on
screen. That makes it impossible to smoothly continue an in-flight
transition when the target changes mid-flight (typical of resize
handlers): the engine would have to interpolate from the previous target
rather than the actual rendered position, producing visual glitches.

`retarget` therefore guarantees a deterministic outcome: the element
snaps to the freshly computed end values with `transition: none`, and
the animation group is marked complete. The element ends up exactly
where the new builder placed it - safe to call repeatedly during a drag
or resize without accumulating partial transitions.

If you need smooth visual continuity instead of a snap, use the `Sub` or
`WAAPI` engines, both of which keep a runtime snapshot of the current
animated value and can interpolate from it.

-}
retarget : AnimState -> (EngineBuilder -> EngineBuilder) -> AnimState
retarget ((AnimState origState _) as animState) build =
    let
        touchedGroups =
            (Builder.process (build origState.builder)).groups

        (AnimState newState newGroups) =
            animate animState build

        snapGroup group =
            group
                |> AnimGroup.setStyles
                    (AnimGroup.getStyles group
                        |> Styles.insert "transition" "none"
                        |> Styles.remove "transition-behavior"
                    )
                |> AnimGroup.setPlayState PlayState.Complete

        snappedGroups =
            AnimGroups.foldl
                (\name _ acc ->
                    case AnimGroups.get name acc of
                        Just group ->
                            AnimGroups.insert name (snapGroup group) acc

                        Nothing ->
                            acc
                )
                newGroups
                touchedGroups
    in
    AnimState newState snappedGroups


toCssPropertyNames : List Builder.ProcessedPropertyConfig -> List String
toCssPropertyNames props =
    List.concatMap
        (\prop ->
            case prop of
                Builder.ProcessedCustomPropertyConfig cssName _ _ ->
                    [ cssName ]

                Builder.ProcessedCustomColorPropertyConfig cssName _ ->
                    [ cssName ]

                Builder.ProcessedOpacityConfig _ ->
                    [ "opacity" ]

                Builder.ProcessedPerspectiveOriginConfig _ ->
                    [ "perspective-origin" ]

                Builder.ProcessedRotateConfig _ ->
                    [ "transform" ]

                Builder.ProcessedScaleConfig _ ->
                    [ "scale" ]

                Builder.ProcessedSizeConfig _ ->
                    [ "width", "height" ]

                Builder.ProcessedSkewConfig _ ->
                    [ "transform" ]

                Builder.ProcessedTranslateConfig _ ->
                    [ "translate" ]
        )
        props


type StartingStylePart
    = TransformPart String
    | CssDeclaration String


propertyToStartingStylePart : Builder.ProcessedPropertyConfig -> Maybe StartingStylePart
propertyToStartingStylePart prop =
    case prop of
        Builder.ProcessedCustomPropertyConfig cssName unit config ->
            config.start
                |> Maybe.map (\start -> CssDeclaration (cssName ++ ": " ++ String.fromFloat start ++ unit ++ ";"))

        Builder.ProcessedCustomColorPropertyConfig cssName config ->
            config.start
                |> Maybe.map (\start -> CssDeclaration (cssName ++ ": " ++ Color.toCssString start ++ ";"))

        Builder.ProcessedOpacityConfig config ->
            config.start
                |> Maybe.map (\start -> CssDeclaration ("opacity: " ++ Opacity.toString start ++ ";"))

        Builder.ProcessedPerspectiveOriginConfig config ->
            config.start
                |> Maybe.map (\start -> CssDeclaration ("perspective-origin: " ++ PerspectiveOrigin.toCssString start ++ ";"))

        Builder.ProcessedRotateConfig config ->
            config.start
                |> Maybe.map (Rotate.toCssString >> TransformPart)

        Builder.ProcessedScaleConfig config ->
            config.start
                |> Maybe.map (Scale.toCssString >> TransformPart)

        Builder.ProcessedSizeConfig config ->
            config.start
                |> Maybe.map
                    (\start ->
                        let
                            ( w, h ) =
                                Size.toTuple start
                        in
                        CssDeclaration ("width: " ++ String.fromFloat w ++ "px; height: " ++ String.fromFloat h ++ "px;")
                    )

        Builder.ProcessedSkewConfig config ->
            config.start
                |> Maybe.map (Skew.toCssString >> TransformPart)

        Builder.ProcessedTranslateConfig config ->
            config.start
                |> Maybe.map (Translate.toCssString >> TransformPart)


extractStartingStyles : List Builder.ProcessedPropertyConfig -> List String
extractStartingStyles properties =
    let
        parts =
            List.filterMap propertyToStartingStylePart properties

        ( transformParts, cssDeclarations ) =
            List.foldl
                (\part ( transforms, declarations ) ->
                    case part of
                        TransformPart t ->
                            ( t :: transforms, declarations )

                        CssDeclaration d ->
                            ( transforms, d :: declarations )
                )
                ( [], [] )
                parts

        transformStyle =
            if List.isEmpty transformParts then
                []

            else
                [ "transform: " ++ String.join " " (List.reverse transformParts) ++ ";" ]
    in
    transformStyle ++ List.reverse cssDeclarations



-- ============================================================
-- EVENTS
-- ============================================================


type alias CurrentTargetId =
    Maybe String


type alias TargetId =
    Maybe String


type AnimEvent
    = Started CurrentTargetId TargetId AnimGroupName
    | Ended CurrentTargetId TargetId AnimGroupName
    | Cancelled CurrentTargetId TargetId AnimGroupName
    | Run CurrentTargetId TargetId AnimGroupName



-- ============================================================
-- UPDATE
-- ============================================================


type AnimMsg
    = GotStarted AnimGroupName CSS.SourceEventData
    | GotEnded AnimGroupName CSS.SourceEventData
    | GotCancelled AnimGroupName CSS.SourceEventData
    | GotRun AnimGroupName CSS.SourceEventData


update : AnimMsg -> AnimState -> ( AnimState, AnimEvent )
update animMsg animState =
    case animMsg of
        GotStarted animGroupName { currentTargetId, targetId } ->
            ( CSS.handleEvent AnimGroup.setPlayState (CSS.TransitionStarted animGroupName) animState
            , Started currentTargetId targetId animGroupName
            )

        GotEnded animGroupName { currentTargetId, targetId } ->
            ( CSS.handleEvent AnimGroup.setPlayState (CSS.TransitionEnded animGroupName) animState
            , Ended currentTargetId targetId animGroupName
            )

        GotRun animGroupName { currentTargetId, targetId } ->
            ( CSS.handleEvent AnimGroup.setPlayState (CSS.TransitionRun animGroupName) animState
            , Run currentTargetId targetId animGroupName
            )

        GotCancelled animGroupName { currentTargetId, targetId } ->
            ( CSS.handleEvent AnimGroup.setPlayState (CSS.TransitionCancelled animGroupName) animState
            , Cancelled currentTargetId targetId animGroupName
            )



-- ============================================================
-- VIEW
-- ============================================================


attributes : AnimGroupName -> AnimState -> List (Html.Attribute msg)
attributes animGroupName ((AnimState _ data) as animState) =
    case AnimGroups.get animGroupName data of
        Nothing ->
            []

        Just animGroup ->
            let
                isComplete =
                    AnimGroup.isComplete animGroup

                discreteExitAttrs =
                    AnimGroup.getDiscreteExit animGroup
                        |> Dict.toList
                        |> List.map
                            (\( prop, { from, to } ) ->
                                if isComplete then
                                    Html.Attributes.style prop to

                                else
                                    Html.Attributes.style prop from
                            )
            in
            CSS.attributes
                []
                AnimGroup.getStyles
                animGroupName
                animState
                ++ discreteExitAttrs


startingStyleNode : AnimState -> Html.Html msg
startingStyleNode ((AnimState _ animGroups) as animState) =
    let
        startingStyles =
            animGroups
                |> AnimGroups.names
                |> List.filterMap (\id -> generateStartingStyle id animState)
                |> String.join "\n"
    in
    if String.isEmpty startingStyles then
        Html.text ""

    else
        Html.node "style" [] <|
            [ Html.text ("@starting-style {\n" ++ startingStyles ++ "\n}") ]


startingStyleNodeFor : AnimGroupName -> AnimState -> Html msg
startingStyleNodeFor animGroupName animState =
    case generateStartingStyle animGroupName animState of
        Just css ->
            Html.node "style" [] <|
                [ Html.text ("@starting-style {\n" ++ css ++ "\n}") ]

        Nothing ->
            Html.text ""


generateStartingStyle : AnimGroupName -> AnimState -> Maybe String
generateStartingStyle animGroupName (AnimState _ animGroups) =
    AnimGroups.get animGroupName animGroups
        |> Maybe.andThen
            (\animGroup ->
                let
                    allStyles =
                        AnimGroup.getStartingStyles animGroup
                in
                if List.isEmpty allStyles then
                    Nothing

                else
                    Just ("  [data-anim-group-name=\"" ++ animGroupName ++ "\"] {\n" ++ String.join "\n" (List.map (\s -> "    " ++ s) allStyles) ++ "\n  }")
            )



-- ============================================================
-- EVENT LISTENERS
-- ============================================================


events : (AnimMsg -> msg) -> List (Html.Attribute msg)
events toMsg =
    [ CSS.onEvent "transitionstart" toMsg GotStarted
    , CSS.onEvent "transitionend" toMsg GotEnded
    , CSS.onEvent "transitionrun" toMsg GotRun
    , CSS.onEvent "transitioncancel" toMsg GotCancelled
    ]


eventsStopPropagation : (AnimMsg -> msg) -> List (Html.Attribute msg)
eventsStopPropagation toMsg =
    [ CSS.onEventStopPropagation "transitionstart" toMsg GotStarted
    , CSS.onEventStopPropagation "transitionend" toMsg GotEnded
    , CSS.onEventStopPropagation "transitionrun" toMsg GotRun
    , CSS.onEventStopPropagation "transitioncancel" toMsg GotCancelled
    ]



-- ============================================================
-- ANIMATION CONTROL
-- ============================================================


stop : AnimGroupName -> AnimState -> AnimState
stop =
    CSS.stop
        AnimGroup.setPlayState
        AnimGroup.isActive
        TransitionStyles.fromProcessedProperties
        setStyles


reset : AnimGroupName -> AnimState -> AnimState
reset =
    CSS.reset
        AnimGroup.setPlayState
        TransitionStyles.fromProcessedProperties
        setStyles


setStyles : Styles -> AnimGroup
setStyles styles =
    AnimGroup.setStyles styles AnimGroup.init
